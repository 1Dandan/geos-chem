{ carboncycle.eqn
-------------------------------------------------------------------------------
DESCRIPTION
-------------------------------------------------------------------------------

GEOS-Chem KPP file containing species and equations for the carbon cycle
chemistry mechanism

===============================================================================
FILE HISTORY
===============================================================================

v14.1.0 (release TBD)
---------------------
(1) Renamed species to remove underscores; updated comments (BMY)

v13.4 (Mar. 31, 2022)
----------------------
(1) Initial version (MSL)

}

#include atoms.kpp

#DEFVAR
CO          = IGNORE;
COfromCH4   = IGNORE;
COfromNMVOC = IGNORE;
CO2         = IGNORE;
CO2fromOH   = IGNORE; 
CH4         = IGNORE;
DUMMY       = IGNORE;

#DEFFIX
// These species are defined as fixed species so that their concentrations
// can be set to either constant values or to fields read via HEMCO.
FixedOH     = IGNORE;
FixedCl     = IGNORE; 
FixedCH4    = IGNORE;
FixedNMVOC  = IGNORE;

#EQUATIONS
//----------------------------------------------------------------------------
// Reactions extracted from ch4_co_co2_mod.F
// Based on Bukosa et al. (2021) with additions for OCS
//
// --- TROP CHEM --- 
//
CH4        + FixedOH = CO    + COfromCH4   : k_Trop(1);                          {= PCO_CH4 via HEMCO or EXP() function, 1/s     } 
CH4        + FixedCl = DUMMY               : 9.60d-12*EXP(-1360.0d0/TEMP)*TROP;  {= Kirschke et al, 2013 (Nature Geosci.), 1/s   }
CO         + FixedOH = CO2   + CO2fromOH   : k_Trop(2);                          {= output of GC_OHCO() function. 1/s            }
FixedNMVOC           = CO    + COfromNMVOC : k_Trop(3);                          {= PCO_NMVOC via HEMCO, molec/cm3/s             }
//
// --- STRAT CHEM ---
//
CH4                  = DUMMY               : k_Strat(1);                         {= CH4LOSS via HEMCO, 1/s                       }
FixedCH4             = CO                  : k_Strat(2);                         {= GMI_PROD_CO via HEMCO v/v/s --> molec/cm3/s  }
CO                   = CO2   + CO2fromOH   : k_Strat(3);                         {= GMI_LOSS_CO via HEMCO. 1/s                   }